//working directory for temporary/intermediate files produced in the workflow processes
workDir = "/mnt/bioinformatics_datasets/nextflow_tmp"
resume = true

//global parameters
params {
    // general options
    sample_sheet                = "test_data/paired_end_sample_sheet.csv"
    strandedness                = ''

    // Input and output options
    download_sra_fqs            = false
    outdir                      = "test_data/test_results"
    publish_dir_mode            = 'copy'

    // STAR specific params
    index                       = '/bioinformatics_resources/genome_references/human/GRCh38/gencode_v48/star/'
    build_index                 = false
    gtf                         = '/bioinformatics_resources/genome_references/human/GRCh38/gencode_v48/ref_files/gencode.v48.primary_assembly.annotation.gtf' // required
    fasta                       = '/bioinformatics_resources/genome_references/human/GRCh38/gencode_v48/ref_files/GRCh38.primary_assembly.genome.fa' // required
    star_ignore_sjdbgtf         = false
    seq_platform                = ''
    seq_center                  = ''

    // trimgalore specific parameters
    trim                        = true

    // RSEQC specific parameters
    rRNA_transcripts              = '/bioinformatics_resources/genome_references/human/GRCh38/rRNA/ensembl_transcript_id_v114.txt'

    // fasterq-dump params
    user_settings               = './ncbi-user-settings.mkfg'

    //MultiQC params
    multiqc_config              = './multiqc_config.yml'
    extra_multiqc_config        = ''
    multiqc_logo                = ''
    rerun_multiqc               = false
}

// Computational resource allocation for the processes run in the workflow
process {
    publishDir = [
        path: { "${params.outdir}/${task.process.tokenize(':')[-1].tokenize('_')[0].toLowerCase()}" },
        mode: params.publish_dir_mode,
        saveAs: { filename -> filename.equals('versions.yml') ? null : filename }
    ]
    errorStrategy = "retry"
    maxRetries = 1
    debug = false

    //STAR-aligner process specific parameters
    //https://www.nextflow.io/docs/latest/process.html#dynamic-computing-resources
    withName: STAR_ALIGN {
        cpus = { 4 * task.attempt }
        memory = { 36.GB * task.attempt }
        label = 'high_priority'
        ext.args = "--quantMode GeneCounts --outSAMtype BAM SortedByCoordinate --readFilesCommand 'gunzip -c' --twopassMode Basic --twopass1readsN -1 --outSAMattributes NH HI NM MD AS nM jM jI XS "
    }

    withName: PICARD_MARKDUPLICATES {
        cpus = { 4 * task.attempt }
        memory = { 16.GB * task.attempt }
        label = 'med_priority'
        ext.prefix = { "${meta.id}.markDuplicates_sorted" }
        ext.args = ''
    }  

    withName: SUBREAD_FEATURECOUNTS {
        cpus = { 4 * task.attempt }
        memory = { 16.GB * task.attempt }
        label = 'high_priority'
        ext.args = '-t exon -g gene_id -O --fraction -J -R BAM --countReadPairs'
    }

    //STAR index process specific parameters
    withName: STAR_GENOMEGENERATE {
        cpus = { 8 * task.attempt }
        memory = { 62.GB * task.attempt }
        label = 'high_priority'
        ext.args = ''
    }

    //Trimgalore process specific parameters
    withName: TRIMGALORE {
        cpus = { 4 * task.attempt }
        memory = { 8.GB * task.attempt }
        ext.args = '--trim-n'
    }
         
    //FastQC process specific parameters
    withName: FASTQC {
        cpus = 2
        memory = 8.GB
        label = 'low_priority'
        ext.args = '--quiet'
    }

    //FastQC process specific parameters after trimming
    withName: FASTQC_TRIM {
        cpus = 2
        memory = 8.GB
        label = 'low_priority'
        ext.prefix = { "${meta.id}.trimmed" }
        ext.args = '--quiet'
    }

    //Samtools flagstat process specific parameters
    withName: SAMTOOLS_FLAGSTAT {
        cpus = 4
        memory = 16.GB
        label = 'low_priority'
        ext.args = '-O tsv'
    }

    //RSEQC process params
    withName: 'rnaseq_count:RSEQC.*' {
        cpus = { 1 * task.attempt }
        memory = { 8.GB * task.attempt }
        ext.args = ''
    }
    /*
    //RSEQC process params
    withName: RSEQC_TIN {
        cpus = { 1 * task.attempt }
        memory = { 8.GB * task.attempt }
        ext.args = ''
    }
    */

    //RSEQC reference files params
    withName: UCSC_GTFTOGENEPRED {
        cpus = { 1 * task.attempt }
        memory = { 4.GB * task.attempt }
        ext.args = "-ignoreGroupsWithoutExons -geneNameAsName2 -genePredExt"
    }

    //MULTIQC process specific parameters
    withName: MULTIQC {
        cpus = 4
        memory = 8.GB
        ext.args = '--fullnames --export --data-dir --verbose'
    }

    //SRA process specific parameters
    withName: SRATOOLS_FASTERQDUMP {
        cpus = { 4 * task.attempt }
        memory = { 24.GB * task.attempt }
        label = 'high_priority'
        ext.args = '--split-files --include-technical --verbose'
        ext.args2 = '--verbose'
    }
}

//Create profiles to easily switch between the different process executors and platforms.
profiles {
    //For running with hyperqueue job scheduler and docker  
    hyperqueue {
        docker { enabled = true }
        // priority scheduling for specific modules
        process {
            executor = 'hq'
            withLabel: high_priority {  clusterOptions = '--priority=5' }
            withLabel: med_priority { clusterOptions = '--priority=1' }
            withLabel: low_priority { clusterOptions = '--priority=-2' }
        }
    }
    //For running on an interactive session with docker  
    docker {
        process.executor = 'local'
        docker.enabled = true
    }
    //For running on an interactive session singularity 
    local_singularity {
        process.executor = 'local'
        singularity.enabled = true
    }
    //For running on an interactive session with apptainer  
    local_apptainer {
        process.executor = 'local'
        apptainer.enabled = true
    }
}

// https://www.nextflow.io/docs/latest/reference/config.html#executor
executor {
  $local {
      cpus = 32
      memory = '125.0 GB'
  }
}


// Set default registry for Apptainer, Docker, Podman and Singularity independent of -profile
// Will not be used unless Apptainer / Docker / Podman / Singularity are enabled
// Set to your registry if you have a mirror of containers
// https://github.com/nf-core/tools/blob/master/nf_core/pipeline-template/nextflow.config
apptainer.registry   = 'quay.io'
docker.registry      = 'quay.io'
podman.registry      = 'quay.io'
singularity.registry = 'quay.io'


//Configs for docker containers on bioinformatics PC
docker {
    engineOptions = ''
    runOptions = "--user \$(id -u):\$(id -g)"
}

singularity {
    autoMounts = true
    cacheDir = "$HOME/containers/singularity"
    runOptions = '--containall --no-home'
}

apptainer {
    autoMounts = true
    cacheDir = "$HOME/containers/apptainer"
    runOptions = '--containall --no-home'
}

//Use personal conda environments on cybertron
conda {
    cacheDir = "$HOME/envs/"
}

//overwrite reports and trace when the workflow is executed again
report {
    overwrite = true
}
trace {
    overwrite = true
}
dag {
    overwrite = true
    direction = 'LR'
}

// https://github.com/nf-core/sarek/blob/dev/nextflow.config#L390
manifest {
    name = 'Meshinchi-Lab/rnaseq_count_nf'
    contributors = [
        [
            name: 'Jenny Leopoldina Smith',
            affiliation: 'OPBG',
            github: '@jennylsmith',
            contribution: ['author', 'maintainer'],
            orcid: '0000-0003-0402-2779'
        ]
    ]
    description = 'Bulk RNA-seq quantification pipeline'
    version = '1.5.0'
}